; ACME 0.96.4
;
; [FRENCH TOUCH]
;
;
; v0.02 	- reverse order
; v0.011	- end added
; v0.01 	- First usable version
;
; TODO	- remove useless labels for final version
;
!cpu 6502
!sl "lbl_shadow.txt"


    *= $50 ; ORG = $50

; =============================================================================
BASE_Y = 7
; =============================================================================
offsetGR		= $26	; +27
; =============================================================================
START
!zone
		; init screens
		JSR $F3E2			; HGR/Erase screen [ROM]
		JSR $FB40			; LORES/Erase Screen [ROM]
		
		; [c = 1 here]

		; display SHADOW text
.loopY		
.modY		LDA #06			
		ADC #BASE_Y		; [c = 1 always because of the CMP #$FF below]
		JSR $F847			; get "offsetGR" (in $26/27) from Y value (in A) [ROM]

		LDA #$55			; gray color
		LDY #39
.loopX		
		LDX #7
.loop1byte
.moddata		LSR+1 DATATEXT+34		; force zp (ACME) 
		BCC +
		STA (offsetGR),Y
+ 		DEY
		DEX
		BPL .loop1byte
		DEC .moddata+1
		CPY #$FF			
		BNE .loopX
		DEC .modY+1
		BPL .loopY
		
		; desynch screen
		; Y = $FF / c = 1 here
mainloop

.modvar1		LDX #255
.loop1		
		STA $C056
.modvar2		BVC +		; BVC xx
		NOP		; 0
		NOP		; 1
		NOP		; 2
		NOP		; -
+		NOP		; 4
		STA $C057
.modvar3		BVC +		
+		STA $C030
		NOP

		DEX
		NOP : NOP		; comment here to test
		BNE .loop1
		DEY
		BNE mainloop
		LDA .modvar2+1
		BEQ +
		LSR		
		STA .modvar2+1
		JMP mainloop

+		STA $C056
.end		JMP .end 		
; =============================================================================
;                            SSSSS   HHHHH AA  AAA DDDD  D OOOOO   WWWWWW
DATATEXT		!byte %00111110,%11001011,%11101111,%00111110,%11000100	
		!byte %00111110,%11001011,%11101111,%10111110,%11000100
		!byte %00110000,%11001011,%00101100,%10110010,%11000100
		!byte %00111110,%11111011,%11101100,%10110010,%11010100
		!byte %00001110,%11111011,%11101100,%10110010,%11111100
		!byte %00111110,%11001011,%00101111,%10111110,%11101100
		!byte %00111110,%11001011,%00101111,%00111110,%11000100
; =============================================================================
FinalSiZE = * - START