; ACME 0.96.4
;
; GROUiK / [FRENCH TOUCH]
; 06/2020
;
; v0.3
;
!cpu 6502
!sl "lbl_shadow.txt"


    *= $60 	; ORG = $60

; =============================================================================
BASE_Y = 7	; + 1 (carry)
; =============================================================================
offsetGR		= $26	; +27
; =============================================================================
START
!zone
		; init screens
		JSR $F3E2			; HGR/Erase screen [ROM]
		JSR $FB40			; LORES/Erase Screen [ROM]
		
		; [c = 1 here]

		; display "SHADOW" text
loopY		
modY		LDA #06			
		ADC #BASE_Y		; [c = 1 always because of the CMP #$FF below]
		JSR $F847			; set "offsetGR" (in $26/27) from Y value (in A) [ROM]

		LDA #$55			; gray color
		LDY #39
loopX		
		LDX #7
loop1byte
moddata		LSR+1 DATATEXT+34		; force zp (ACME) 
		BCC +
		STA (offsetGR),Y
+ 		DEY
		DEX
		BPL loop1byte
		DEC moddata+1
		CPY #$FF			
		BNE loopX
		DEC modY+1
		BPL loopY
		
		; desynch screen
		; Y = $FF / c = 1 here
mainloop
		LDX #255
loop1		
		STA $C056
modvar1		BVC +		; BVC xx
		NOP		; 0
		NOP		; 1
		NOP		; 2
+		NOP		; 3
		ASL $C057
		BVC +		
+		LDA ($AC,X)	
		STA $C030
		DEX
		BNE loop1
		DEY
		BNE mainloop
		DEC modvar1+1
		BPL mainloop

		; end part
		; Y = 0 here
		
		LDX #80	
loope	
-		LDA $C019
		BPL -
		LDA $C056,Y
		LDA #68
		JSR $FCA8			; WAIT routine => (26+27*68+5*68^2)/2 cycles [ROM]
		TYA
		EOR #01
		TAY
		DEX
		BNE loope

		RTS			; return to BASIC (actually fucked! but who cares ?!)
; =============================================================================
;                               SSS  S HHHH A  AAA DDDD   OOOO WW  WWW    
DATATEXT		!byte %00000111,%10100101,%11100001,%01111010,%00100000	
		!byte %00000100,%10100101,%00100001,%01001010,%00100000
		!byte %00000100,%00100101,%00100001,%01001010,%00100000
		!byte %00000111,%10111101,%11101111,%01001010,%10100000
		!byte %00000000,%10100101,%00101001,%01001010,%10100000
		!byte %00000100,%10100101,%00101001,%01001010,%10100000
		!byte %00000111,%10100101,%00101111,%01111011,%11100000
; =============================================================================
SiZE = * - START
!if (SiZE >$80) {!warn "Too large guy, try again!"} else {!warn SiZE}